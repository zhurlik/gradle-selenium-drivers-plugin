apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'codenarc'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:$publishPluginVersion"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:$coverallsPlugin"
    }
}

repositories {
    mavenCentral()
}

codenarc {
    toolVersion = "$codenarcVersion"
    ignoreFailures = false
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    doFirst {
        classDirectories = classDirectories.collect {
            fileTree(it) {
                exclude '**/*_getToolsLocation*'
                exclude '**/*choco*'
                exclude '**/*Choco*'
                exclude '**/*_closure3'
                exclude '**/*_closure4'
                exclude '**/*InstallIE11*'
                exclude '**/*extractDmg*'
            }
        }.sum()
    }

    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

test {
    jacoco {
        excludes += [
                '**/*_getToolsLocation*',
                '**/*choco*',
                '**/*Choco*',
                '**/*_closure3',
                '**/*_closure4',
                '**/*InstallIE11*',
                '**/*extractDmg*'
        ]
    }
}

group = 'com.github.zhurlik'

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

// Integration tests
sourceSets {
    integTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/groovy')
        }

        resources.srcDir file('src/integTest/resources')
    }
}

idea {
    module {
        //add integration test source dirs to IDEA
        testSourceDirs += [file('src/integTest/groovy'), file('src/integTest/resources')]
        scopes.TEST.plus += [configurations.integTestCompile, configurations.integTestRuntime]
    }
}

task integTest(type: Test) {
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
}
// Integration tests

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile group: 'junit', name: 'junit', version: "$junitVersion"

    testCompile group: 'com.codeborne', name: 'phantomjsdriver', version: "$phantomjsDriverVersion"
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: "$seleniumVersion"
}